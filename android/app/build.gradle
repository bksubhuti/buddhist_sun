plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('keystore.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    println("❗ keystore.properties NOT FOUND")
}

def apiKeyProperties = new Properties()
def apiKeyPropertiesFile = rootProject.file('api_keys.properties')
if (apiKeyPropertiesFile.exists()) {
    apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
} else {
    println("❗ api_keys.properties NOT FOUND")
}

android {
    namespace "com.bksubhuti.buddhistsun"
    compileSdk = 36
    ndkVersion "27.2.12479018"

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        applicationId "com.bksubhuti.buddhistsun"
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion

        // Version comes ONLY from pubspec.yaml
        versionCode flutter.versionCode
        versionName flutter.versionName

        resValue "string", "google_maps_key", (apiKeyProperties['GOOGLE_MAPS_API_KEY'] ?: "")
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source "../.."
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}
